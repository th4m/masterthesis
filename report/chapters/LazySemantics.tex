\chapter{Lazy semantics of CakeML}
\label{lazySem}
The second step of this thesis, after translating the strict semantics of CakeML
to Haskell, is to define lazy semantics that the compiler will be based on.
The lazy semantics defined in Haskell is designed to delay evaluation by
storing partially evaluated expressions in thunks. This chapter will describe
the definition of the lazy semantics.

\section{Definition}
\label{lazySem:impl}
%% Modified two modules: semantic primitives and evaluate (mainly) 
%% SemPrim was modified to remove state from certain functions
%% SemPrim was modified to add Thunk to V
%% This definition of lazy evaluation is pure
%% evaluate is redefined in evaluateLazy
%% Doesn't take state
%% Tries to evaluate as little as possible
%% When possible, evaluate takes one (or more) steps in evaluation,
%% by e.g. applying a function from semantic primitives, and then
%% wrapping the resulting expression in a thunk and returning it
%% Implemented force to fully evaluate expressions
%% Attempt 1 was faulty. Could return wrong environment
%% Attempt 2 was successful. Forced certain subexpressions instead.

The task of defining the lazy semantics for CakeML consists of defining a new
\texttt{evaluate} function (called \texttt{evaluateLazy} from now on) and
appropriate functions in the semantic primitives.
In order to implement laziness, it is also necessary to incorporate the idea of
thunks, as described in Section~\ref{back:lazySem}. As such, thunks are added
as the \texttt{Thunk} constructor in the \texttt{V} datatype. This makes
it a value that can be returned as a result by
\texttt{evaluateLazy}. The main purpose of using thunks is to suspend partially
evaluated expressions within. To further evaluate suspended expressions,
additional information, contained in the environment, is required.
The \texttt{Thunk} constructor is thus added to the \texttt{V} type:

\begin{alltt}
  data V =
    LitV Lit
    ...
    | Thunk (Environment V) Exp
\end{alltt}

The function \texttt{evaluateLazy}, the lazy version of \texttt{evaluate}, is
implemented with the goal of evaluating expressions as little as possible. While
the type of \texttt{evaluateLazy} needs to stay similar to
\texttt{evaluate}, minor changes are made; mainly, the state is omitted
altogether in order to keep the evaluation pure. The type of
\texttt{evaluateLazy} is thus declared as follows:

\begin{alltt}
  evaluateLazy :: Environment V -> [Exp] -> Result [V] V
\end{alltt}

\noindent With the addition of the \texttt{Thunk} constructor, \texttt{evaluateLazy}
returns either a thunk or any other value.

Another vital function that needs to be implemented is \texttt{force}.
This function is designed to take a value and pattern match for the
\texttt{Thunk} constructor. When the \texttt{Thunk} case is entered,
the expression wrapped inside the thunk is extracted and lazily evaluated until
an actual value is yielded. When any other value is caught in the pattern matching,
the value is simply returned as a result.
When the evaluation of an expression ends at top level, the returned value
must be an actual non-thunk value, as in the strict semantics of CakeML.
The function \texttt{force} is defined as such:

\begin{alltt}
  force :: V -> Result [V] V
  force (Thunk env e) =
    case evaluateLazy env [e] of
      RVal [Thunk env' e'] -> force (Thunk env' e')
      res -> res
  force v = RVal [v]
\end{alltt}

Using the definition of thunks and \texttt{force}, the definition of
\texttt{evaluateLazy} can be written.
The following sections will describe how it is defined in two attempts,
one that is erroneous and one that is correct.

\subsection{Attempt 1}
The first (erroneous) attempt of implementing \texttt{evaluateLazy} uses a naive
idea of evaluating expressions as little as possible. This is mainly seen in
expressions that contains sub-expressions, e.g. \texttt{If e1 e2 e3}. The first
version of \texttt{evaluateLazy} on \texttt{If} applies
\texttt{evaluateLazy} on \texttt{e1} and checks if it returns a thunk or a
value. If it is a thunk, wrapped together with a partially evaluated expresion
\texttt{e1'} and a potentially different environment \texttt{env'}, it will
create and return a new thunk \texttt{Thunk env' (If e1' e2 e3)}. If any other
type of value \texttt{v} is returned, it applies \texttt{do\_if} to yield either
\texttt{e2} or \texttt{e3} as the expression \texttt{e}. This in turn,
assuming that \texttt{v} is valid, creates and returns a new thunk containing
\texttt{e} and the environment that was given as input to \texttt{evaluateLazy}.

\begin{alltt}
  evaluateLazy st env [If e1 e2 e3]   =
    case evaluateLazy st env [e1] of
      (st', RVal vs)   -> case head vs of
        Thunk env' e1' -> (st', RVal [Thunk env' (If e1' e2 e3)])
        v              -> case do_if v e2 e3 of
          Just e  -> (st', RVal [Thunk env e])
          Nothing -> (st', RErr (RAbort RType_Error))
      res -> res
\end{alltt}

The problem with this implementation is that the environment is passed along
in each iteration. The strict \texttt{evaluate} evaluates \texttt{e} with
the input environment. This act of passing on an altered environment is an
unwanted process, as \texttt{e1} may assign some variables that were only
intended to be temporary for the evaluation of \texttt{e1}. The strict
\texttt{evaluate} does not exhibit this behaviour, but instead keeps the
evaluation of \texttt{e1} in its own scope.

\subsection{Attempt 2}
\label{lazySem:att2}
The second attempt of implementing \texttt{evaluateLazy} creates a
delayed evaluation without altering more data than wanted. This
is achieved by forcing certain sub-expressions instead of leaving them
suspended. Forcing these sub-expressions produces the value that is needed
to continue the evaluation process. The forcing process is made easier by
creating a function called \texttt{evalAndForce} that applies
\texttt{evaluateLazy} on the head of the given list of expressions and
environment, and forces the resulting value.

\begin{alltt}
  evalAndForce :: Environment V -> [Exp] -> Result [V] V
  evalAndForce _env []    = RVal []
  evalAndForce env (e:es) =
    case evaluateLazy env [e] of
      RVal v -> case force (head v) of
        RVal val ->
          case evalAndForce env es of
            RVal vs -> RVal ((head val):vs)
            res -> res
        res -> res
      res -> res
\end{alltt}

Taking the \texttt{If e1 e2 e3} case as an example again, the condition
\texttt{e1} is forced to produce a (hopefully boolean) value, which is run
through \texttt{do\_if}. The resulting expression, which is the branch that is
to be evaluated in the next step, is wrapped in a thunk and returned as a
result.

\begin{alltt}
  evaluateLazy env [If e1 e2 e3]   =
    case evalAndForce env [e1] of
      RVal v ->
        case do_if (head v) e2 e3 of
          Just e  -> RVal [Thunk env e]
          Nothing -> RErr (RAbort RType_Error)
      res -> res
\end{alltt}

This new idea of forcing sub-expressions is applied to all cases of the
pattern matching in \texttt{evaluateLazy}.

\section{Example of evaluation}
\label{lazy:example}
Similarly to Section~\ref{strict:example}, this section will describe how the
lazy evaluation of \texttt{take} works. The evaluation described will be using
\texttt{force} so that the top-level result will not be a thunk value.

The first step of evaluating \texttt{take} is the evaluation of the
\texttt{LetRec} expression. It will create a \texttt{RecClosure} and return
the following expression (using the variable \texttt{take}) as a thunk.
When forced, it can be used in the same manner as with the strict semantics:
applying the \texttt{RecClosure} that is bound to the variable \texttt{take}
to an integer literal with \texttt{App OpApp}. The resulting \texttt{Fun}
expression is evaluated to a \texttt{Closure} similarly to the strict semantics.
When applied to a list \texttt{ls} with \texttt{App OpApp}, the \texttt{If}
expression is evaluated. The expression that represents the boolean condition
(equality of \texttt{n} and 0) is
forced to yield a non-thunk value, which is used as an argument to the
\texttt{do\_if} helper function. The result when \texttt{n} is equal to 0 is the
expression that returns a \texttt{nil} constructor. In the other case, the list
\texttt{ls} is pattern matched with \texttt{Mat} to check if it is either a
\texttt{::} or \texttt{nil} constructor. When \texttt{ls} is evaluated here,
both the head element and the tail of the list (applied as argument to
\texttt{take} with \texttt{n} subtracted by 1) are put into thunks. This means
that forcing constructors by normal means will not yield
a fully evaluated constructor value. Instead, a different \texttt{force} function
is defined to specifically force the contents of constructors. This is done by
simply pattern matching on constructor values to check for thunks that are then
evaluated and forced to yield a fully evaluated list.


\section{Testing the definition}
%% Tested the implementation
%% Used the list of expressions that was tested for the strict semantics
%% Created a comparison function that evaluated each expression with both
%% strict and lazy semantics and checked if the results were identical
%% Also tested certain expressions that would end up in bottoms (non-terminating loops)
%% Both test cases were successful

Similarly to the strict semantics, unit testing is performed on the lazy
semantics by using the same dummy environment and expressions. 
The result from the lazy evaluation is compared to what the strict evaluation
yields to make sure that they are equivalent. An additional test method is
applied for lazy evaluation as well. This testing consists of checking for
termination in evaluation of expressions that normally do not terminate with
the strict semantics. These two test methods will be described in this section.

\subsection{Comparing results}
\label{lazy:compres}
For comparing results between lazy and strict evaluation, a function, called
\texttt{compareEval} is defined. The function \texttt{compareEval} takes a list
of expressions and applies \texttt{evaluate} and \texttt{evaluateLazy}
in separate passes. Both evaluation processes use the same dummy environment.
Evaluation with an empty state (called \texttt{empty\_st}) and
a dummy environment (called \texttt{ex\_env}) is simplified by defining a
function for strict evaluation:

\begin{alltt}
  ex e = evaluate empty_st ex_env [e]
\end{alltt}

\noindent Similarly, evaluation for lazy evaluation with \texttt{ex\_env} is
simplified by defining a function for lazy evaluation:

\begin{alltt}
  exForce e = evalAndForce ex_env [e]
\end{alltt}

\noindent \texttt{evalAndForce} is naturally used, as a pure value is expected
to be compared with the strict evaluation. With the definition of \texttt{ex}
and \texttt{exForce}, \texttt{compareEval} is defined as such:

\begin{alltt}
  compareEval expList = map exForce expList == map (snd . ex) expList
\end{alltt}

By collecting a list of expressions, testing is performed by simply running
\texttt{compareEval} on a given list. This is done whenever the semantics is
changed and needs to be tested to control that the results would not change.

One expression that is not compared is the \texttt{Con} expression. The lazy
evaluation of \texttt{Con} expressions produces thunks of the constructors'
arguments. When forced, they are not evaluated. This behaviour allows the
possibility of infinite data structures, such as lists, to exist.
Keeping the contents of a list as thunks can save computation time if,
for example, \texttt{take} is applied to a significantly large list, but only
takes a minimal amount of elements. However, thunks can also
act as a double-edged sword. For example, in the case where a full list is to be
shown, the strict semantics will be faster to generate the list. This is because
of the additional steps that the lazy semantics takes to produce thunks only to
force them to non-thunk values, which the strict semantics produces directly.
This example shows that lazy semantics is not always optimal and that different
situations may require different semantics for the best performance.

\subsection{Testing termination}
As laziness brings the feature of delaying evaluation of expressions, certain
expressions that would lead to non-termination when using the strict semantics
should terminate when using the lazy semantics. For example, when the expression
\texttt{Let x e1 e2} has a non-terminating expression for \texttt{e1}, the
strict semantics will not terminate on the evaluation of \texttt{e1}. However,
the lazy semantics will simply wrap \texttt{e1} in a thunk and bind it to the
variable \texttt{x}, allowing the evaluation to continue to evaluating
\texttt{e2}.

This test method consists of creating CakeML expressions that exhibit the
behaviour explained above. One such expression is defined
and called \texttt{termExp}. A non-terminating expression \texttt{inf} is also
defined to be used together with \texttt{termExp}.
The expression
\texttt{inf} is defined to create a recursive function \texttt{fun}
that simply calls itself to create an infinite loop.
The definition of \texttt{termExp} uses
\texttt{inf} by binding it to a variable \texttt{var} in a \texttt{Let}
expression before returning a string literal "OK".

\begin{alltt}
  termExp =
    Let (Just "var") (inf) (Literal (StrLit "OK"))
    where inf =
      LetRec [("fun", "par", App OpApp [Var (Short "fun")
                                       ,Literal (IntLit 0)])]
      (App OpApp [Var (Short "fun"), Literal (IntLit 0)])
\end{alltt}

\noindent Running \texttt{termExp} with the strict semantics naturally causes a
non-terminating loop, while running \texttt{termExp} with the lazy semantics
results in the literal value "OK" being returned, as expected.

With the test results showing positive results, the lazy semantics can be deemed
to work as intended. This signals that the groundwork for the compiler is
complete.
